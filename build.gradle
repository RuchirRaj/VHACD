buildscript {
    ext.kotlin_version = '1.4.10'
    ext.ktor_version = '1.4.1'
    ext.koin_version = '2.1.6'
    ext.junit_version = '5.7.0'

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/" // for klint
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'org.jlleitschuh.gradle.ktlint' version '9.4.1'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'jacoco'

mainClassName = 'com.unity.template.ApplicationKt'

defaultTasks 'run'

repositories {
    jcenter()
    mavenCentral()
}

compileKotlin {
    // micrometer uses jdk 1.8 features, so we need to use that version here
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-core-jvm:$ktor_version"
    implementation "io.ktor:ktor-client-jetty:$ktor_version"
    implementation "io.ktor:ktor-client-json-jvm:$ktor_version"
    implementation "io.ktor:ktor-jackson:$ktor_version"
    implementation "io.ktor:ktor-metrics:$ktor_version"
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-websockets:$ktor_version"

    implementation "org.koin:koin-ktor:$koin_version"

    implementation 'io.github.microutils:kotlin-logging:2.0.3'
    
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'ch.qos.logback.contrib:logback-json-classic:0.1.5'
    implementation 'ch.qos.logback.contrib:logback-jackson:0.1.5'

    implementation "org.jetbrains.exposed:exposed:0.17.7"
    implementation "org.postgresql:postgresql:42.2.18"
    implementation "org.flywaydb:flyway-core:7.0.0"

    implementation 'io.nats:jnats:2.8.0'

    implementation 'io.micrometer:micrometer-registry-prometheus:1.6.0'

    testImplementation "io.ktor:ktor-client-mock:$ktor_version"
    testImplementation "io.ktor:ktor-client-mock-jvm:$ktor_version"
    testImplementation "io.ktor:ktor-server-tests:$ktor_version"

    testImplementation "org.koin:koin-test:$koin_version"

    testImplementation "io.mockk:mockk:1.10.2"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
}

shadowJar {
    classifier = ''
    archiveVersion = ''
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}
